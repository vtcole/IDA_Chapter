  Data:
    File is PerspectiveTakingData.csv;
  Variable:
    Names are
      ID study age
        male white task_1 task_2 task_3 
        task_4 task_5 task_6 task_7 task_8;
    Missing ARE ALL (-55555);
    Usevariables are task_1 task_2 task_3 task_4
        task_5 task_6 task_7 task_8
        age white male study_1 study_2
        ;
    Constraint = age study_1 study_2;
    Categorical = task_1 task_2 task_3 task_4
        task_5 task_6 task_7;
  define:
    Study_1 = 0;
    If (study == 1) THEN Study_1 = 1;
    Study_2 = 0;
    If (study == 2) THEN Study_2 = 1;
    Study_3 = 0;
    If (study == 3) THEN Study_3 = 1;
  analysis:
  estimator = MLR;
  algorithm = integration;
  model:
  f by task_1* (l1)
       task_2* (l2)
       task_3* (l3)
       task_4* (l4)
       task_5* (l5)
       task_6* (l6)
       task_7* (l7)
       task_8* (l8);
       f(v);
  task_1 ON age;
  task_3 ON age;
  task_5 ON study_2;
  task_6 ON age white;
  task_7 ON male study_2;
  task_8 ON age study_1 study_2;

  f ON age study_1 study_2; 

  model constraint:
  new ( v_1 
        v_2 
        v_3
        l8_0
        l8_1
        l8_3);
  v = exp(v_1*age + v_2*study_1 + v_3*study_2);
  l8 = l8_0 + l8_1*age + l8_3*study_2;